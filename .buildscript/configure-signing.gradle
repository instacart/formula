apply plugin: "signing"
apply plugin: "com.vanniktech.maven.publish"

if (project.hasProperty('releaseBuild')) {
    println "configuring signing key"

    Properties properties = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    }

    signing {
        // Support both env vars (CI) and local.properties (local dev)
        def signingKeyEnv = System.getenv('SIGNING_KEY')
        def signingKey
        if (signingKeyEnv) {
            // CI: key content directly from env var
            signingKey = signingKeyEnv
        } else {
            // Local: read from file specified in local.properties
            def signingKeyFilename = properties.signingKey
            signingKey = "${project.rootProject.file(signingKeyFilename).newDataInputStream()}"
        }
        def signingPassword = System.getenv('SIGNING_PASSWORD') ?: properties.signingPassword

        useInMemoryPgpKeys(signingKey, signingPassword)
    }

    mavenPublish {
        targets {
            // Modify the existing uploadArchives task
            uploadArchives {
                repositoryUsername = System.getenv('SONATYPE_NEXUS_USERNAME') ?: properties.SONATYPE_NEXUS_USERNAME
                repositoryPassword = System.getenv('SONATYPE_NEXUS_PASSWORD') ?: properties.SONATYPE_NEXUS_PASSWORD
            }
        }
    }
} else {
    mavenPublish {
        releaseSigningEnabled = false
    }
}
