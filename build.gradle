apply from: 'dependencies.gradle'
apply plugin: "com.github.ben-manes.versions"

buildscript {
    ext {
        kotlinVersion = '1.8.10'

        versions = [:]
        libraries = [:]
        constants = [
                minSdk      : 21,
                targetSdk   : 30,
                compileSdk  : 30
        ]
    }

    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        // We need to fork the plugin to fix an incorrect usage of tasks.withType(Test)
        // https://github.com/vanniktech/gradle-android-junit-jacoco-plugin/compare/master...Laimiux:gradle-android-junit-jacoco-plugin:laimonas/gradle-8?expand=1
        classpath("com.github.Jawnnypoo:gradle-android-junit-jacoco-plugin:61a9cc0129")
        classpath "com.github.ben-manes:gradle-versions-plugin:0.38.0"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.8.10"
        classpath "org.jetbrains.dokka:android-documentation-plugin:1.4.32"
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.14.2"
    }
}

apply plugin: "org.jetbrains.dokka"

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    tasks.withType(org.jetbrains.dokka.gradle.DokkaTaskPartial).configureEach {
        dokkaSourceSets.named("main") {
            configuration {
                jdkVersion.set(8)
                skipDeprecated.set(true)
                skipEmptyPackages.set(true)
                reportUndocumented.set(false)
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: "com.vanniktech.android.junit.jacoco"
junitJacoco {
    jacocoVersion = '0.8.8'
    excludes = [
            // Defaults
            '**/androidx/**/*.*',
            '**/*$$*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*$Lambda$*.*', // Jacoco can not handle several "$" in class name.
            '**/*$inlined$*.*', // Kotlin specific, Jacoco can not handle several "$" in class name.
            '**/com/instacart/formula/test/**/*.*'
    ]
    ignoreProjects = [
            "samples/*"
    ]
    includeNoLocationClasses = true
    includeInstrumentationCoverageInMergedReport = true
}

apply from: 'gradle/merge-reports.gradle'

tasks.register("install") {
    def publishTasks = []
    project.subprojects.forEach { project ->
        def installTask = project.tasks.findByName("install") ?: project.tasks.findByName("publishToMavenLocal")
        if (installTask != null) {
            publishTasks.add(installTask)
        }
    }
    dependsOn publishTasks
}

