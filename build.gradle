apply from: 'dependencies.gradle'
apply plugin: "com.github.ben-manes.versions"

buildscript {
    ext {
        kotlinVersion = '1.5.21'

        versions = [:]
        libraries = [:]
        constants = [
                minSdk      : 21,
                targetSdk   : 30,
                compileSdk  : 30
        ]
    }

    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.38.0"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.4.32"
        classpath "org.jetbrains.dokka:android-documentation-plugin:1.4.32"
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.14.2"
    }
}

apply plugin: "org.jetbrains.dokka"

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    tasks.withType(org.jetbrains.dokka.gradle.DokkaTaskPartial).configureEach {
        dokkaSourceSets.named("main") {
            configuration {
                jdkVersion.set(8)
                skipDeprecated.set(true)
                skipEmptyPackages.set(true)
                reportUndocumented.set(false)
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: "com.vanniktech.android.junit.jacoco"
junitJacoco {
    // WARNING! Don't upgrade unless https://github.com/Malinskiy/danger-jacoco/issues/10 is fixed
    jacocoVersion = '0.8.2'
    excludes = [
            // Defaults
            '**/androidx/**/*.*',
            '**/*$$*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*$Lambda$*.*', // Jacoco can not handle several "$" in class name.
            '**/*$inlined$*.*', // Kotlin specific, Jacoco can not handle several "$" in class name.
            '**/com/instacart/formula/test/**/*.*'
    ]
    ignoreProjects = [
            "samples/*"
    ]
    includeNoLocationClasses = true
    includeInstrumentationCoverageInMergedReport = true
}

apply from: 'gradle/merge-reports.gradle'

tasks.register("install") {
    def publishTasks = []
    project.subprojects.forEach { project ->
        def installTask = project.tasks.findByName("install") ?: project.tasks.findByName("publishToMavenLocal")
        if (installTask != null) {
            publishTasks.add(installTask)
        }
    }
    dependsOn publishTasks
}

