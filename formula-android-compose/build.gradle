apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply from: rootProject.file('.buildscript/configure-signing.gradle')
apply from: rootProject.file('.buildscript/jacoco-workaround.gradle')

android {
    namespace "com.instacart.formula.android.compose"
    compileSdkVersion constants.compileSdk

    defaultConfig {
        minSdkVersion constants.minSdk
        targetSdkVersion constants.targetSdk
    }

    buildFeatures {
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    composeOptions {
        kotlinCompilerVersion constants.kotlinVersion
        kotlinCompilerExtensionVersion composeVersion
    }

}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    setArchiveClassifier('sources')
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.api
    classpath += configurations.implementation
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    setArchiveClassifier('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    api project(":formula-android")
    api libraries.androidx.compose.ui

    implementation libraries.androidx.compose.rxjava3

    testImplementation libraries.androidx.test.rules
    testImplementation libraries.androidx.test.runner
    testImplementation libraries.androidx.test.espresso.core

    testImplementation libraries.truth
    testImplementation libraries.kotlinReflect
}
